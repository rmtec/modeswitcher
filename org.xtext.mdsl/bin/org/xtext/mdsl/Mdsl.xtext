grammar org.xtext.mdsl.Mdsl with org.eclipse.xtext.common.Terminals

generate mdsl "http://www.xtext.org/mdsl/Mdsl"

Model: (declarations+=Declaration)*;
Declaration : System | Software | Mode | Service | Action | Rule | Event;

System :
	'System' name=ID 'has' 'following' 'modes'
		(modes+=[Mode] (',' modes+=[Mode])*)?
	('operatingSystem' operatingSystem=SUPPORTED_OPERATING_SYSTEMS)?
	('distribution' distribution=SUPPORTED_DISTRIBUTIONS)?
	('release' release=SUPPORTED_RELEASES)?
;

enum SUPPORTED_OPERATING_SYSTEMS:
	Windows|Linux
;
enum SUPPORTED_DISTRIBUTIONS:
	Debian|Ubuntu|Other
;
enum SUPPORTED_RELEASES:
	Buster|Other
;

Software : 
	'Software' name=ID //software with a dot in the name should be specified in the package e.g. php7 package "php7.3"
	('vendor' vendor=STRING)?
	('product' product=STRING)?
	('version' version=STRING)?
	('package' package=STRING)?
;

Mode :
	'Mode' name=ID ('extends' superMode=[Mode])?
	'description' description=STRING
	'priority' priority=INT
	'startServices' (startServices+=[Service] 
		(',' startServices+=[Service])*
	)?
	'stopServices' (stopServices+=[Service] 
		(',' stopServices+=[Service])*
	)?
	(enabled=Enabled)?
	('exchangeable' 'with' alternativeModes+=[Mode] (',' alternativeModes+=[Mode])*)?
;

enum Enabled:
	true | false //default: true. disabled for e.g. maintenance mode
;

Service :
	'Service' name=ID
	('usesSoftware' usesSoftware+=[Software] (',' usesSoftware+=[Software])*)?
	'startActions' startActions+=ActionWithParams 
		(',' startActions+=ActionWithParams)*
	'stopActions' stopActions+=ActionWithParams
		(',' stopActions+=ActionWithParams)*
;

ActionWithParams : 
	action=[Action]'('(params+=STRING (',' params+=STRING)*)?')'
;

Action :
	'Action' name=ID ('extends' superAction=[Action])?
	('description' description=STRING)?
	(('shellCmd' shellCmd=STRING)|('params' params+=STRING (',' params+=STRING)*))
;

Rule :
	'Rule' description=STRING
		'if' if+=[Event] 
		('and' 'current' 'mode' 'in' '('currentMode+=[Mode] 
			(',' modes+=[Mode])*')'
		)?
		'then' then=[Mode]
		('else' else=[Mode])?
;
		
Event : 
	'Event' name=ID
	'description' description=STRING
;